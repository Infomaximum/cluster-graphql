plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.checkerframework" version "0.6.46" //https://github.com/kelloggm/checkerframework-gradle-plugin
}
if (project.hasProperty('ossrhUsername')) {
    apply from: 'publish.gradle'
}

group 'com.infomaximum'
version '1.1.11'

ext.moduleName = 'com.infomaximum.cluster.graphql'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources {
            srcDirs "src/main/resources"
        }
    }
    test {
        java.srcDir 'src/test/java'
    }
}


dependencies {

    implementation 'com.infomaximum:cluster:1.1.11'
//    implementation project(':cluster')

    implementation('com.graphql-java:graphql-java:19.11') {
        exclude group: 'org.reactivestreams', module: 'reactive-streams'
        exclude group: 'com.graphql-java', module: 'java-dataloader'//from exclude org.slf4j
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation('com.graphql-java:java-dataloader:3.2.2') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation('org.reflections:reflections:0.10.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.javassist', module: 'javassist'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'org.javassist:javassist:3.30.2-GA'//Более новая зависимость с поддержкой java модулей

    implementation('io.reactivex.rxjava2:rxjava:2.2.21') {
        exclude group: 'org.reactivestreams', module: 'reactive-streams'
    }
    implementation 'org.reactivestreams:reactive-streams:1.0.4'

    implementation 'org.slf4j:slf4j-api:2.0.16'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'

    testImplementation 'ch.qos.logback:logback-core:1.5.12'
    testImplementation 'ch.qos.logback:logback-classic:1.5.12'
}
